# Обработка событий на карте. Грузит карту, ставит в дефолтное место маркер, отображает места и спрашивает текущее местоположение
$ ->
  map.init()
  map.put_marker_on_current()
  map.show_places()
  map.cluster_markers()
  map.get_current_location()


# Параметры и функции карты
map =
  dom: document.getElementById("map-canvas")
  last_valid_center: app.current_location
  options:
    center: app.current_location,
    disableDefaultUI: true,
    zoom: 13
    minZoom: 4
    styles: [{stylers: [{ gamma: 0.79 },{ saturation: -30 }]}]
  last_valid_center: undefined
  bounds: undefined
  markers: []


  # Ставит маркер на координаты
  # @param location [LatLng] координаты точки
  set_current_location: (location) ->
    app.google_map.setCenter location
    app.current_location = location
    map.put_marker_on_current()
    map.reset_markers()


  # В случае успеха определения местоположения юзера, обновляет карту
  # @param location [LatLng] координаты местоположения юзера
  nav_geo_success: (location) ->
    map.reset_location()
    current_location = new google.maps.LatLng(location.coords.latitude, location.coords.longitude)
    map.set_current_location(current_location)


  # При определении местоположения сбрасывает предыдущее значение и кнопку валить
  reset_location: ->
    app.current_marker.setMap(null)
    unless app.path_to_evac == undefined
      app.path_to_evac.setMap(null)
      $("#evac_btn").removeClass("hidden")
      $("#share_btn").addClass("hidden")


  # В случае ошибки получения гео локации выводи лажу
  nav_geo_error: ->
    console.log "ти где??? :["


  # Определяет местоположение юзера по ip
  get_location_by_ip: ->
    $.get "http://ip-api.com/json",(data) ->
      if data.status == "success"
        map.reset_location()
        current_location = new google.maps.LatLng(data.lat, data.lon)
        map.set_current_location(current_location)
      else
        map.nav_geo_error()


  # Спрашивает у пользователя его местоположение
  get_current_location: ->
    if geo_position_js.init()
      geo_position_js.getCurrentPosition(map.nav_geo_success,map.get_location_by_ip, {enableHighAccuracy: true, timeout: 10000})
    else
      # Если не получается - то по ip
      map.get_location_by_ip()


  # Добавляет маркер с определенными параметрами к текущему месту
  put_marker_on_current: ->
    current_loc_marker_options =
      position: app.current_location
      icon: "<%= asset_path('map/current.png') %>"
      map: app.google_map
    app.current_marker = new google.maps.Marker current_loc_marker_options


  # Получает расстояние до каждой точки эвакуации и рисует их на карте
  show_places: ->
    places.objects.forEach(places.get_distance)
    places.objects.forEach(places.render)


  # Обновляет маркеры для подготовки к новому текущему местоположению
  reset_markers: ->
    places.objects.forEach(places.get_distance)
    places.objects.forEach(places.update_infobox)

                       
  # Проверяет, вышел ли пользователь за пределы границ карты. Передвигает в предыдущий "чистый" центер, если да, или записывает новый "чистый" центр
  checkBounds: ->
    map.last_valid_center = app.google_map.getCenter() if map.bounds.contains(app.google_map.getCenter())
    app.google_map.panTo map.last_valid_center


  # Группирует маркеры вместе в один, если они рядом
  cluster_markers: ->
    markerCluster_styles = [{
      url: "<%= asset_path('map/evac.png') %>",
      height: 35,
      width: 35,
      textSize: 1
    }]
    cluster = new MarkerClusterer(app.google_map, map.markers, {styles: markerCluster_styles})
    cluster_infobox = new InfoBox(app.infobox_options)

    # При наведении по кластереру открывает инфобокс и тултип для мобил
    google.maps.event.addListener cluster, 'mouseover', (this_clusterer) ->
      fake_marker = map.get_fake_marker(this_clusterer)

      cluster_infobox.setContent(map.clusterer_infobox())
      cluster_infobox.open(app.google_map, fake_marker)
      fake_marker.setMap(null)
      map.update_tooltip(map.clusterer_infobox())

    # И закрывает
    google.maps.event.addListener cluster, 'mouseout', ->
      cluster_infobox.close()
      $("#tooltips, .news_evac").toggleClass("hidden")

    # При отдалении тоже закрывает
    google.maps.event.addListener app.google_map, "center_changed", ->
      cluster_infobox.close()


  # Устанавливает контент у инфобокса кластерера маркеров
  # @return [DOM] содержимое инфобокса
  clusterer_infobox: ->
    infobox_content = "
      <div class='evac_popup'>
        <div class='left'>
          <div class='arrow-left arrow hidden-xs'></div>
          <div class='multy evac_icon'></div>
        </div>
        <div class='right'>
          <h5>Точки эвакуации</h5>
          <h4>Сразу несколько!</h4>
        </div>
      </div>
      "


  # Создает фейковый маркер для размещения там иконки кластерера
  # @param clusterer [MarkerClusterer] кластерер точек на карте
  # @return [google.maps.Marker] фейковый маркер
  get_fake_marker: (clusterer) ->
    fake_marker_options =
      position: clusterer.getCenter()
      map: app.google_map
    new google.maps.Marker(fake_marker_options)


  # Отображает тултип и обновляет контент попапа на мобилах
  # @param infobox_content [DOM] содержимое инфобокса
  update_tooltip: (infobox_content) ->
    setTimeout (->
      $(".evac_popup").fadeIn(200)
    ), 100
    $("#tooltips").toggleClass("hidden").html infobox_content
    $(".news_evac").toggleClass("hidden")


  # Инициализация карты и обозначение границ
  init: ->
    app.google_map = new google.maps.Map(@.dom, @.options)    
    map.bounds = new google.maps.LatLngBounds(new google.maps.LatLng(41.2, 19.8), new google.maps.LatLng(77.792, 179.9999))

    # При передвижении по карте проверяет, находимся ли мы еще в пределах границы
    google.maps.event.addListener app.google_map, "center_changed", ->
      map.checkBounds()


# Параметры и функции мест
places =      
  objects:
    gon.places

  # Отображает место на карте: создает маркер для него
  # @param place [Object] место
  render:(place) ->
    place_position = new google.maps.LatLng(place.lat, place.lng)
    place_marker_options =
      position: place_position
      map: app.google_map
      icon: "<%= asset_path('map/evac.png') %>"
    place_marker = new google.maps.Marker(place_marker_options)
    place.marker = place_marker
    map.markers.push(place_marker)

    places.create_infobox(place, place_marker)


  # Создает инфобокс для места
  # @param place [Object] место
  # @param place_marker [google.maps.Marker] маркер места
  create_infobox:(place, place_marker) ->
    place.infobox = new InfoBox(app.infobox_options)
    place.infobox.setContent places.get_infobox(place)

    # При наведении по маркеру открывает инфобокс
    google.maps.event.addListener place_marker, 'mouseover', ->
      if !app.time_to_evac || app.places.closest != place
        place.infobox.open(app.google_map, this)
        map.update_tooltip(place.infobox.getContent())

    # И закрывает
    google.maps.event.addListener place_marker, 'mouseout', ->
      place.infobox.close()
      if !app.time_to_evac || app.places.closest != place
        $("#tooltips, .news_evac").toggleClass("hidden")


    # При отдалении тоже закрывает
    google.maps.event.addListener app.google_map, "center_changed", ->
      place.infobox.close()


  # Устанавливает контент у инфобокса места
  # @param place [Object] место
  # @return [DOM] содержимое инфобокса
  get_infobox: (place) ->
    infobox_content = "
      <div class='evac_popup'>
        <div class='left'>
          <div class='arrow-left arrow hidden-xs'></div>
          <div class='" + app.places.types[place.type_id].icon + " evac_icon'></div>
        </div>
        <div class='right'>
          <h5>Точка эвакуации</h5>
          <h4>#{place.name}</h4>
          <p class='distance'>#{place.distance.toFixed(1)} км</p>
        </div>
      </div>
      "


  # Обновляет контент у инфобокса места
  # @param place [Object] место
  update_infobox: (place) ->
    place.infobox = new InfoBox(app.infobox_options)
    place.infobox.setContent places.get_infobox(place)


  # Перевод в радианы
  # @param x [Integer] число
  # @return [Integer] радианы
  rad:(x) ->
    x * Math.PI / 180


  # Добавляет расстояние от места до текущего положения
  # @param place [Object] место
  get_distance:(place) ->
    cur_loc_lat = app.current_location.lat()
    cur_loc_lng = app.current_location.lng()
    earth_radius = 6371
    place_lat = place.lat
    place_lng = place.lng

    dLat = places.rad(place_lat - cur_loc_lat)
    dLong = places.rad(place_lng - cur_loc_lng)
    a = Math.sin(dLat / 2) * Math.sin(dLat / 2) + Math.cos(places.rad(cur_loc_lat)) * Math.cos(places.rad(cur_loc_lat)) * Math.sin(dLong / 2) * Math.sin(dLong / 2)
    c = 2 * Math.atan2(Math.sqrt(a), Math.sqrt(1 - a))
    place.distance = earth_radius * c

    places.closest_check(place)


  # Проверяет, является ли это место ближайшим к текущему и записывает, если да
  # @param place [Object] место
  closest_check:(place) ->
    if app.places.closest == undefined || app.places.closest.distance > place.distance
      app.places.closest = place