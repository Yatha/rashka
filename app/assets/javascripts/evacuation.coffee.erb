# Обработка эвакуации
$ ->
  evacuation.init()


# Параметры и функции, связанные с эвакуацией
evacuation =
  dom_element: $("#evac_btn")
  hidden_polyline: undefined 
  hidden_polyline2: undefined 
  escape_marker: undefined
  path_length: undefined


  # Запуск слушателя при нажатии
  init: ->
    # При нажатии строит маршрут до ближайшего места от текущего, отображает кнопку рассказать и увеличивает статистику
    $(evacuation.dom_element).click ->
      evacuation.get_directions(app.current_location, app.places.closest)
      evacuation.share_mode_on()
      evacuation.evac_stat_increase()


  # Скрывает кнопку валить и показывает кнопку рассказать
  share_mode_on: ->
    @.dom_element.addClass("hidden")
    $("#share_btn").removeClass("hidden")


  # Запускает метод увеличения статистики
  evac_stat_increase: ->
    $.ajax "../stats/evacuate",
      type: "POST"
      dataType: "json"


  # Строит маршрут до ближайшего места
  # @param start_point [google.maps.LatLng] место начала
  # @param end_place [Object] место конца
  get_directions: (start_point, end_place) ->
    end_point = new google.maps.LatLng(end_place.lat, end_place.lng)

    directions_service =new google.maps.DirectionsService()
    directions_service_options = evacuation.get_directions_service_options(start_point, end_point)

    directions_service.route(directions_service_options, (response, status) ->
      # В случае успеха запроса выводит маршрут и гонит по нему петра
      if status == google.maps.DirectionsStatus.OK
        evacuation.show_directions(response)
#        app.current_marker.setIcon("<%= asset_path('map/mover.png') %>")
        evacuation.create_end_place_infobox(end_place)
        evacuation.adjust_center(response)
        evacuation.escape(response)
    )

    
  # Запускает анимацию сваливания из рашки, создавая невидимый маршрут и расставляя на нем временные точки для маркера
  # @note запускается из evacuation.get_directions
  # @param response [Маршрут] маршрут до места
  escape: (response) ->
    route = response.routes[0]
    legs = route.legs

    evacuation.prepare_escape_polyline(legs)
    evacuation.add_evac_segments(legs)
    evacuation.start_animation()


  # Расставляет на маршруте сваливания временные точки для анимации маркера
  # @note запускается из evacuation.escape
  # @param legs [route.legs] уточненный путь маршрута
  add_evac_segments: (legs) ->
    i = 0
    while i < legs.length
      steps = legs[i].steps
      j = 0
      while j < steps.length
        nextSegment = steps[j].path
        k = 0
        while k < nextSegment.length
          evacuation.hidden_polyline.getPath().push nextSegment[k]
          k++
        j++
      i++


  # Создает невидимый маршрут, на котором будут расставлены маркеры для анимации
  # @note запускается из evacuation.escape
  # @param legs [route.legs] уточненный путь маршрута
  prepare_escape_polyline: (legs) ->
    evacuation.hidden_polyline = new google.maps.Polyline(
      path: []
    )

    evacuation.escape_marker = new google.maps.Marker(
      position: legs[0].start_location
      map: app.google_map
      icon: "<%= asset_path('map/mover.png') %>"
    )


  # Какая-то лютая магия с махинациями с маркерами для того, чтобы анимация сваливания работала ок
  # @note запускается из evacuation.animate
  # @param distance [Number] расстояние, на который нужно передвинуть маркер сваливания
  poly_magic: (distance) ->

    lastVertex = 1
    evacuation.hidden_polyline2 = new google.maps.Polyline([evacuation.hidden_polyline.getPath().getAt(lastVertex - 1)])  if evacuation.hidden_polyline2.getPath().getLength() > 20

    if evacuation.hidden_polyline.GetIndexAtDistance(distance) < lastVertex + 2
      evacuation.hidden_polyline2.getPath().removeAt evacuation.hidden_polyline2.getPath().getLength() - 1  if evacuation.hidden_polyline2.getPath().getLength() > 1
      evacuation.hidden_polyline2.getPath().insertAt evacuation.hidden_polyline2.getPath().getLength(), evacuation.hidden_polyline.GetPointAtDistance(distance)
    else
      end_point = new google.maps.LatLng(app.places.closest.lat, app.places.closest.lng)

      evacuation.hidden_polyline2.getPath().insertAt evacuation.hidden_polyline2.getPath().getLength(), end_point


  # Узнает, куда нужно передвинуть маркер во время анимации и пытается это сделать
  # @note запускается из evacuation.start_animation
  # @param distance [Number] расстояние, на который нужно передвинуть маркер сваливания
  animate: (distance) ->
    point = evacuation.hidden_polyline.GetPointAtDistance(distance)

    # Если еще не дошли до конца
    if point?
      evacuation.move_marker(point, distance)
    else
      evacuation.finish_animation()


  # Передвигает маркер
  # @note запускается из evacuation.animate
  # @param point [LatLng] точка в пути на определнном расстоянии
  # @param distance [Number] расстояние, на который нужно передвинуть маркер сваливания
  move_marker: (point, distance) ->
    app.google_map.panTo point
    evacuation.escape_marker.setPosition point
    evacuation.poly_magic(distance)

    step = evacuation.path_length / 35
    animationTimeout = window.setTimeout (->
      evacuation.animate(distance + step)
    ), 100


  # Заканчивает анимацию сваливания
  # @note запускается из evacuation.animate
  finish_animation: ->
    end_point = new google.maps.LatLng(app.places.closest.lat, app.places.closest.lng)

    app.google_map.panTo end_point
    evacuation.escape_marker.setPosition end_point
    evacuation.escape_marker.setMap(null)
    audio = new Audio("<%= asset_path('hallelujah.ogg') %>")
    audio.play()


  # Начало анимации сваливания
  # @note запускается из evacuation.escape
  start_animation: ->
    evacuation.hidden_polyline.setMap app.google_map
    eol = evacuation.hidden_polyline.Distance()
    app.google_map.setCenter evacuation.hidden_polyline.getPath().getAt(0)
    evacuation.hidden_polyline2 = new google.maps.Polyline(
      path: [evacuation.hidden_polyline.getPath().getAt(0)]
    )
    evacuation.path_length = evacuation.hidden_polyline.Distance()

    setTimeout (->
      evacuation.animate(500)
    ), 200


  # Адаптирует центр карты, чтобы правильно отображался весь маршрут
  # @param response [Маршрут] маршрут до места
  adjust_center: (response) ->
    bounds = response.routes[0].bounds
    app.google_map.fitBounds(bounds);
    app.google_map.setCenter(bounds.getCenter())
    setTimeout(->
      app.google_map.setZoom(app.google_map.getZoom() - 1)
    ,200)


  # Отображение инфобокса/(тултипа на мобиле) у места
  # @param end_point [google.maps.Lang] место конца маршрута
  create_end_place_infobox: (end_place) ->
    place_infobox = new InfoBox(app.infobox_options)
    infobox_content = evacuation.get_evac_infobox(end_place)

    place_infobox.setContent(infobox_content)
    setTimeout (->
      $(".evac_popup").fadeIn(200)
    ), 100

    place_infobox.open(app.google_map, end_place.marker)
    $("#tooltips").addClass("hidden")
    $(".news_evac").removeClass("hidden").html infobox_content


  # Устанавливает контент у инфобокса места, к которому будет идти эвакуация
  # @param place [Object] место
  # @return [DOM] содержимое инфобокса
  get_evac_infobox: (place) ->
    infobox_content = "
      <div class='evac_popup escape'>
        <div class='left'>
          <img src=" + "<%= asset_path('arrow-left.png') %>" + " class='arrow hidden-xs'>
          <img src='"+ app.places.types[place.type_id].icon + "'class='evac_icon'>
        </div>
        <div class='right'>
          <h5>Точка эвакуации</h5>
          <h4>#{place.name}</h4>
          <p class='time_to_evac'>Собирай манатки, ведь тебе до него всего #{app.time_to_evac}!</p>
        </div>
      </div>
      "


  # Создает рендерер для маршрута
  # @return [google.maps.DirectionsRenderer] рендерер маршрута
  get_directions_renderer: () ->
    directions_renderer_options =
      map: app.google_map
      suppressMarkers: true
      polylineOptions:{strokeColor:'#24c94b', strokeWeight: "6"}
    new google.maps.DirectionsRenderer(directions_renderer_options)


  # Настройки отображения маршрута
  # @param start_point [google.maps.LatLng] место начала
  # @param end_point [google.maps.LatLng] место конца маршрута
  # @return [Object] настройки маршрута
  get_directions_service_options: (start_point, end_point) ->
    origin: start_point
    destination: end_point
    travelMode: google.maps.TravelMode.DRIVING


  # Отображение маршрута на карте и запоминание времени маршрута
  # @param response [Маршрут] маршрут до места
  show_directions: (response) ->
    directions_renderer = evacuation.get_directions_renderer()
    directions_renderer.setDirections(response)
    app.directions_renderer = directions_renderer
    app.time_to_evac = response.routes[0].legs[0].duration.text